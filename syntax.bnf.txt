<syntax> ::= <expr>
<ws> ::= <ws-char> | <ws-char> <ws>
<ws-char> ::= " " | "\n" | "\t"
<opt-ws> ::= "" | <ws>
<expr> ::= <opt-ws> <expr-value> <opt-ws>
<expr-value> ::= <list> | <quote> | <unquote> | <const> | <literal> | <symbol>
<exprs> ::= "" | <expr> <exprs>
<list> ::= "(" <exprs> ")"
<quote> ::= "\" <expr-value>
<unquote> ::= "," <expr-value>
<const> ::= "##" <user-const> | "#" <lang-const>
<user-const> ::= <VALIDSYMBOL>
<lang-const> ::= "t" | "f" | "nil"
<literal> ::= <dec-literal> | <dec-literal> "a" | <bin-literal> | <bin-literal> "a" | <str-literal>
<dec-literal> ::= "-" <dec-literal> | "0"..."9" | "0"..."9" <dec-literal>
<bin-literal> ::= ":" <bin-literal-value>
<bin-literal-value> ::= "0" | "0" <bin-literal-value> | "1" | "1" <bin-literal-value>
<str-literal> ::= "'" <str-contents> "'" | "~" "'" <str-contents> "'"
<str-contents> ::= "" | <str-unit> <str-contents>
<str-unit> ::= !{"'","\\"} | "\\" !{}
<symbol> ::= <VALIDSYMBOL>
<VALIDSYMBOL> ::= <valid-symbol-char> | <valid-symbol-char> <VALIDSYMBOL>
<valid-symbol-char> ::= "A"..."Z" | "a"..."z" | "-" | "+" | "*" | "/"

